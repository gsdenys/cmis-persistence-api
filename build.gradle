group 'org.cmis.cpa'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: "jacoco"

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}


dependencies {
    compile group: 'org.apache.chemistry.opencmis', name: 'chemistry-opencmis-client-impl', version: '1.0.0'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'com.github.gsdenys', name: 'junit-server-cmis', version: '1.1.2'

}

//coverage
jacoco {
    toolVersion = "0.7.6.201602180812"
    reportsDir = file("$buildDir/reports/jacoco")
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled true
        html.destination "${buildDir}/reports/jacoco/html"
    }
}


//maven central upload
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar

    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}


uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: System.env.USER ?: ossrhUsername, password: System.env.PASSWORD ?: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: System.env.USER ?: ossrhUsername, password: System.env.PASSWORD ?: ossrhPassword)
            }

            pom.project {
                name 'cmis-persistence-api'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'CPA - CMIS Persistence API - The esiest way to use CMIS integration for java programers'
                url 'http://github.com/gsdenys/cmis-persistence-api'

                scm {
                    connection 'scm:git:git@github.com:gsdenys/cmis-persistence-api.git'
                    developerConnection 'scm:git:git@github.com:gsdenys/cmis-persistence-api.git'
                    url 'git@github.com:gsdenys/cmis-persistence-api.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'gsdenys'
                        name 'Denys G. Santos'
                        email 'gsdenys@gmail.com'
                    }
                }
            }
        }
    }
}